/*

All about Bees, Bees products and beekeeping

*/

#modloaded forestry gendustry

import crafttweaker.item.IIngredient;
import crafttweaker.item.IItemStack;
import crafttweaker.item.WeightedItemStack;
import crafttweaker.liquid.ILiquidStack;
import scripts.craft.grid.Grid;

// ğŸŸ ğŸŸ¡ğŸŸ¢ğŸŸ£ğŸŸ¤ğŸŸ¥ğŸŸ¦ğŸŸ§ğŸŸ¨ğŸŸ©ğŸŸªğŸŸ«

# Common ingredients
static g as IItemStack[string] = {
	'ğŸŸ¡' : <forestry:beeswax>,
	'ğŸ’›' : <forestry:honey_drop>,
	'ğŸ¤' : <forestry:honeydew>,
	'ğŸŸ¤' : <forestry:propolis>,
	'ğŸŸ ' : <forestry:propolis:1>, # Sticky Propolis
	'ğŸ”´' : <forestry:refractory_wax>,
	'ğŸ”·' : <forestry:phosphor>,
	'ğŸ’œ' : <randomthings:ingredient:1>,
	'ğŸ¤' : <minecraft:ghast_tear>,
	'âšª' : <thaumcraft:quicksilver>,
} as IItemStack[string];

# ----------------------------------------------
# Bees rework
# ----------------------------------------------

# Phosphor as melting mechannics
for input,output in {
	<ore:dustPyrotheum>      : <fluid:pyrotheum> * 250,
	<ore:dustCryotheum>      : <fluid:cryotheum> * 250,
	<ore:dustAerotheum>      : <fluid:aerotheum> * 250,
	<ore:dustPetrotheum>     : <fluid:petrotheum> * 250,
	<ore:blockRedstone>      : <fluid:redstone> * 900,
	<ore:glowstone>          : <fluid:glowstone> * 1000,
	<ore:materialEnderPearl> : <fluid:ender> * 250,

	<ore:blockCoal>
	|<ore:blockGraphite>     : <fluid:coal> * 900,
} as ILiquidStack[IIngredient]  {
	scripts.processWork.work(["ForestrySqueezer"], null, [g['ğŸ”·'], input], null, null,  [output], null, null);
}

# Buff silk
recipes.removeByRecipeName("forestry:silk_to_string");
recipes.addShapeless("silk to string", <minecraft:string> * 32, [<forestry:crafting_material:2>]);

# [Cobweb]*16 from [Silk Wisp]
recipes.removeByRecipeName("forestry:silk_wisp_to_web");
craft.make(<minecraft:web> * 16, ["pretty",
  "S   S",
  "  S  ",
  "S   S"], {
  "S": <forestry:crafting_material:2>, # Silk Wisp
});

# Buff Pulsating mesh output
mods.forestry.Carpenter.removeRecipe(<minecraft:ender_pearl>);
mods.forestry.Carpenter.addRecipe(<actuallyadditions:block_misc:6> * 4,
	Grid(["AA","AA"], {A:<forestry:crafting_material:1>}).shaped(), 60);

# Simplify Alviery because its feels too grindy for so low value
# [Alveary*14] from [Impregnated Casing][+1]
craft.remake(<forestry:alveary.plain> * 14, ["pretty",
  "S S S",
  "S I S",
  "S S S"], {
  "S": <forestry:crafting_material:6>, # Scented Paneling
  "I": <forestry:impregnated_casing>,  # Impregnated Casing
});

# Way cheaper instead using Royal Jelly and Pollen Cluster to descrease grind
# [Scented Paneling] from [Honeydew][+2]
val scentPanelGrid = Grid(["pretty",
	"  H  ",
	"# # #",
	"B   B"], {
	"B": <ore:itemBeeswax>,  # Beeswax
	"#": <ore:plankWood>,    # Oak Wood Planks
	"H": <ore:dropHoneydew>, # Honeydew
}).shaped();
mods.forestry.Carpenter.removeRecipe(<forestry:crafting_material:6>);
mods.forestry.Carpenter.addRecipe(<forestry:crafting_material:6>, scentPanelGrid, 40, <liquid:for.honey> * 500);
mods.forestry.Carpenter.addRecipe(<forestry:crafting_material:6>, scentPanelGrid, 40, <liquid:honey>     * 500);


# ---------------------------
# Remake old combs
function reprocessComb(comb as IItemStack, outputs as WeightedItemStack[]) as void {
	mods.forestry.Centrifuge.removeRecipe(comb);
	mods.forestry.Centrifuge.addRecipe(outputs, comb, 60);

	if(!comb.matches(<forestry:bee_combs:9>))
		mods.thermalexpansion.Centrifuge.removeRecipe(comb);
	mods.thermalexpansion.Centrifuge.addRecipe(outputs, comb, null, 2000);
}

# [Cocoa_Comb]
reprocessComb(<forestry:bee_combs:1>, [
  g['ğŸŸ¡'] % 80, # Beeswax
  <minecraft:dye:3> % 50, # Cocoa
  <nuclearcraft:milk_chocolate> % 20,
]);

# [Stringy_Comb]
reprocessComb(<forestry:bee_combs:3>, [
	g['ğŸŸ '],
	g['ğŸ’›'] % 40,  # Honey Drop
]);

# [Frozen_Comb]
reprocessComb(<forestry:bee_combs:4>, [
	g['ğŸŸ¡'] % 80,     # Beeswax
	g['ğŸ’›'] % 70,  # Honey Drop
	<forestry:pollen:1> % 20,     # Crystalline Pollen Cluster
	<thermalfoundation:material:2048> % 20,
]);

# [Dripping_Comb]
reprocessComb(<forestry:bee_combs:5>, [
	g['ğŸ¤'] % 100, # Honeydew
	g['ğŸ’›'] % 40, # Honey Drop
	<thermalfoundation:material:1> % 20, # Gold Dust
]);

# [Parched_Comb]
reprocessComb(<forestry:bee_combs:7>, [
	(<minecraft:blaze_powder> * 2) % 45, # Blaze Powder
	g['ğŸŸ¡'] % 100,  # Beeswax
	g['ğŸ’›'] % 90, # Honey Drop
]);

# [Powdery_Comb]
reprocessComb(<forestry:bee_combs:10>, [
	g['ğŸŸ¡'] % 20,     # Beeswaxer
	g['ğŸ’›'] % 20,  # Honey Drop
	(<minecraft:gunpowder> * 4) % 100, # Gunpowder
	<nuclearcraft:marshmallow> % 30,
]);

# [Mossy_Comb]
reprocessComb(<forestry:bee_combs:15>, [
	g['ğŸŸ¡'] % 100, # Beeswax
	g['ğŸ’›'] % 90, # Honey Drop
	<tconstruct:materials:18> % 100,
]);

# [Irradiated_Comb]
reprocessComb(<forestry:bee_combs:9>, [
  g['ğŸŸ¡'] % 50, # Beeswax
  <ic2:nuclear> % 60,   # Enriched Uranium Nuclear Fuel
  <ic2:nuclear:4> % 20, # MOX Nuclear Fuel
]);

# [Wheaten_Comb]
reprocessComb(<forestry:bee_combs:14>, [
  g['ğŸŸ¡'] % 80, # Beeswax
  (<harvestcraft:flouritem> * 3) % 100,
  <nuclearcraft:graham_cracker> % 80, # Graham Cracker
]);

# [Mellow_Comb]
reprocessComb(<forestry:bee_combs:16>, [
	g['ğŸŸ¡'] % 20, # Beeswax
	g['ğŸ¤'] % 60, # Honeydew
	<ore:dustSoularium>.firstItem % 40,
]);

# Add missed beecombs to JEI (Why they even missed??)
val bee_comb_def = <forestry:bee_combs>.definition;
for i in [1, 8, 9 ,10] as int[] {
  mods.jei.JEI.addItem(bee_comb_def.makeStack(i));
}
mods.jei.JEI.addItem(<forestry:propolis:2>); # Pulsating Propolis

# Missed propolis recycle recipe
scripts.process.squeeze([<forestry:propolis>], null, "only: TECentrifuge", <ic2:misc_resource:4>);

# ---------------------------


/*

New Bees mutations

Generated with https://arthursonzogni.com/Diagon/
by data from config/gendustry/bees.cfg

â”Œâ”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”
â”‚Boggyâ”‚â”‚Agrarianâ”‚â”‚Frugalâ”‚â”‚Derpiousâ”‚â”‚Farmerlyâ”‚â”‚Heroicâ”‚â”‚Miryâ”‚â”‚Modestâ”‚â”‚Phantasmalâ”‚â”‚Ruralâ”‚â”‚Sinisterâ”‚â”‚Wintryâ”‚â”‚Valiantâ”‚
â””â”¬â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”˜â””â”¬â”¬â”€â”€â”€â”€â”€â”€â”˜â””â”¬â”¬â”€â”€â”€â”€â”€â”€â”˜â””â”¬â”¬â”€â”€â”€â”¬â”˜â””â”¬â”¬â”€â”¬â”˜â””â”¬â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”˜â””â”¬â”¬â”€â”€â”€â”€â”€â”˜
 â”‚      â”‚         â”‚       â”‚â”‚      â”Œâ”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”‚â”€â”€â”‚â”‚â”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”˜           â”‚      â”‚         â”‚       â”‚â”‚
â”Œâ”˜      â”‚ â”Œâ”€â”€â”€â”€â”€â”€â”€â”˜â”Œâ”€â”€â”€â”€â”€â”€â”˜â”‚      â”‚ â”‚â”‚      â”Œâ”€â”‚â”‚â”€â”€â”€â”‚â”€â”€â”‚â”˜ â”‚  â”‚                   â”‚      â”‚         â”‚       â”‚â”‚
â”‚â”Œâ”€â”€â”€â”€â”€â”€â”˜ â”‚       â”Œâ”‚â”€â”€â”€â”€â”€â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”‚â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”‚â”€â”‚â”‚â”€â”€â”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜      â”‚         â”‚       â”‚â”‚
â”‚â”‚â”Œâ”€â”€â”€â”€â”€â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”˜     â”Œâ”‚â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”‚â”€â”‚â”‚â”€â”€â”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜         â”‚       â”‚â”‚
â”‚â”‚â”‚      â”Œâ”‚â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”˜â”‚     â”Œâ”‚â”€â”‚â”‚â”€â”€â”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜       â”‚â”‚
â”‚â”‚â”‚      â”‚â”‚       â”‚â”‚     â”Œâ”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”˜     â”‚â”‚ â”‚â”‚   â”‚â”Œâ”€â”‚â”€â”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜â”‚
â”‚â”‚â”‚      â”‚â”‚  â”Œâ”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”˜â”‚   â”‚â”‚ â”‚  â”‚  â”‚ â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
â”‚â”‚â”‚      â”‚â”‚ â”Œâ”‚â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”˜   â”‚â”‚ â”‚  â”‚  â”‚ â”‚
â”‚â”‚â”‚      â”‚â”‚ â”‚â”‚    â”‚â”‚    â”Œâ”‚â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”˜  â”‚  â”‚ â”‚
â”‚â”‚â”‚      â”‚â”‚ â”‚â”‚    â”‚â”‚    â”‚â”‚ â”Œâ”€â”€â”€â”€â”€â”‚â”˜â”Œâ”€â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”‚â”€â”€â”˜ â”‚
â”‚â”‚â”‚â”Œâ”€â”€â”€â”€â”€vvâ”â”‚â”‚â”Œâ”€â”€â”€vvâ”â”Œâ”€â”€vvâ”â”‚â”Œâ”€â”€â”€â”€vâ”€vâ”â”Œâ”€â”€â”€â”€â”€vvâ”â”Œâ”€â”€â”€â”€vvâ”â”Œâ”€â”€vâ”€â”€â”€â”€vâ”
â”‚â”‚â”‚â”‚Elysianâ”‚â”‚â”‚â”‚Wackyâ”‚â”‚Oozyâ”‚â”‚â”‚Gallantâ”‚â”‚Chilledâ”‚â”‚Potterâ”‚â”‚Tinsmithâ”‚
â”‚â”‚â”‚â””â”€â”¬â”€â”€â”€â”€â”€â”˜â”‚â”‚â””â”€â”¬â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”˜â”‚â””â”¬â”¬â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”€â”€â”˜
â”‚â”‚â”‚  â”‚      â”‚â””â”€â”€â”‚â”€â”   â”‚    â”‚ â”‚â”‚       â”‚
â”‚â”‚â””â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”‚â”  â”‚ â”‚   â”‚    â”‚ â”‚â”‚       â”‚
â”‚â””â”€â”€â”â”‚   â”Œâ”€â”€â”‚â”‚â”€â”€â”‚â”€â”‚â”€â”€â”€â”‚â”€â”€â”€â”€â”‚â”€â”˜â”‚       â”‚
â””â”€â”€â”â”‚â”‚   â”‚â”Œâ”€â”‚â”‚â”€â”€â”‚â”€â”‚â”€â”€â”€â”˜   â”Œâ”˜  â”‚       â”‚
â”Œâ”€â”€â”‚â”‚â”˜   â”‚â”‚ â”‚â”‚  â”‚ â”‚      â”Œâ”‚â”€â”€â”€â”˜       â”‚
â”‚â”Œâ”€â”‚â”‚â”€â”€â”€â”€â”‚â”‚â”€â”˜â”‚ â”Œâ”‚â”€â”‚â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
â”‚â”‚â”Œvvâ”€â”€â”€â”€vvâ”â”Œvâ”€vvâ”â”‚â”Œâ”€â”€â”€â”€â”€vvâ”
â”‚â”‚â”‚Scrappy â”‚â”‚Dullâ”‚â”‚â”‚Paughtyâ”‚
â”‚â”‚â””â”€â”€â”€â”€â”€â”€â”¬â”¬â”˜â””â”¬â”€â”€â”¬â”˜â”‚â””â”¬â”€â”€â”€â”€â”¬â”€â”˜
â”‚â”‚      â”Œâ”‚â”‚â”€â”€â”˜  â”‚ â”‚ â”‚    â”‚
â”‚â”‚      â”‚â”‚â””â”   â”Œâ”‚â”€â”‚â”€â”˜    â”‚
â”‚â””â”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”‚â”€â”€â”€â”‚â”‚â”€â”‚â”€â”    â”‚
â”‚â”Œâ”€â”€â”€â”€â”€â”€vvâ”â”‚â”Œâ”€â”€vvâ”â”‚â”Œvâ”€â”€â”€â”€vâ”
â”‚â”‚Egoisticâ”‚â”‚â”‚Vainâ”‚â”‚â”‚Tinkerâ”‚
â”‚â””â”€â”€â”€â”€â”€â”€â”€â”€â”˜â”‚â””â”¬â”€â”¬â”¬â”˜â”‚â””â”€â”€â”€â”€â”€â”€â”˜
â”‚          â”‚â”Œâ”˜ â””â”‚â”€â”‚â”€â”€â”€â”€â”€â”
â””â”         â””â”‚â”€â”€â”â”‚ â””â”€â”€â”€â”€â”€â”‚â”
â”Œvâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€vâ”â”Œvvâ”€â”€â”€â”€â”€â”â”Œvvâ”€â”€â”€â”€â”€â”
â”‚Narcissisticâ”‚â”‚Selfishâ”‚â”‚Artisanâ”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”€â”˜
*/

/*

	Helper class to make several new Bees recipes at once

*/
zenClass BeeHelper {

	# Bees final production
	val beesOutputs as IItemStack[][] = null;

	# Current bee index
	var index as int = -1 as int;
	var beesNames as string[] = [];

	# Constructor should be called once to register all recipes
	zenConstructor(g as IItemStack[string]) {
		beesOutputs = [
				#  Wax  ,Honeydrop, Main output
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <enderio:item_alloy_nugget:9>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <thermalfoundation:material:770>, <ic2:dust:25>],
				[g['ğŸŸ¡'] ,g['ğŸ¤'], <immersiveengineering:material:18>, <nuclearcraft:dust:8>],
				[g['ğŸŸ¡'] ,g['ğŸ¤'], <extendedcrafting:material:37>],
				[g['ğŸŸ¡'] ,g['âšª'], <appliedenergistics2:material:5>],
				[g['ğŸ”´'] ,g['ğŸ¤'], <libvulpes:productnugget:7>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <ic2:crafting:23> * 64],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("psigem"), shard("emeradic_actadd_plustic"), shard("integrationforegoing.plastic"), shard("amethyst")],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("desert_myrmex"), shard("jungle_myrmex"), shard("steeleaf")],
				[g['ğŸ”·'] ,g['ğŸ’›'], <botania:manaresource:16>, <botania:manaresource:1>, <botania:manaresource:2>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <actuallyadditions:item_crystal_empowered:2>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <mysticalagradditions:insanium:5>],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("litherite"), shard("erodium"), shard("kyronite"), shard("wyvern_plustic")],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("pladium"), shard("ionite"), shard("aethium"), shard("boron_nitride")],
				[g['ğŸŸ¡'] ,g['ğŸ’œ'], <extendedcrafting:material:49>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <storagedrawers:upgrade_storage:3>],
			] as IItemStack[][];
	}

	function build() {
			next("Wacky"   ); scripts.process.melt(c['ğŸ’§'], <fluid:construction_alloy> * 144, "No exceptions");
			next("Chilled" ); /* Defined in bees.cfg */
			next("Oozy"    ); scripts.process.crush(c['ğŸ’§'], c['ğŸ¯'], "No exceptions", [currOutList()[3]], [0.25], {bonusType: "MULTIPLY_OUTPUT"});
			next("Elysian" ); scripts.process.compress(c['ğŸ’§'], c['ğŸ¯'], "No exceptions");
			next("Gallant" ); furnace.addRecipe(c['ğŸ¯'] * 2, c['ğŸ’§']);
			next("Dull"    ); scripts.process.saw(c['âš™ï¸'], c['ğŸ¯'] * 7, "except: shapeless");
			next("Scrappy" ); scripts.process.crush(c['ğŸ’§'], c['ğŸ¯'], "No exceptions", [c['ğŸ¯'], c['ğŸ¯'], c['ğŸ¯']], [0.50, 0.25, 0.10], {bonusType: "MULTIPLY_OUTPUT"});
			next("Potter"  ); addTinkersCentrifuges();
			next("Tinsmith"); addTinkersCentrifuges();
			next("Ğ aughty" ); mods.botania.ManaInfusion.addInfusion(currOutList()[2], c['ğŸ’§'], 100); mods.botania.ManaInfusion.addAlchemy(currOutList()[3], c['ğŸ’§'], 200); mods.botania.ManaInfusion.addConjuration(currOutList()[4], c['ğŸ’§'], 500); 
			next("Egoistic"); mods.actuallyadditions.AtomicReconstructor.addRecipe(c['ğŸ¯'], c['ğŸ’§'], 10000);
			next("Vain"    ); scripts.process.fill(c['ğŸ’§'],  <liquid:base_essence>,  c['ğŸ¯'],  "Except: Casting DryingBasin MechanicalDryingBasin");
			next("Tinker"  ); mods.thermalexpansion.Centrifuge.removeRecipe(c['âš™ï¸']); addTinkersCentrifuges();
			next("Artisan" ); mods.thermalexpansion.Centrifuge.removeRecipe(c['âš™ï¸']); addTinkersCentrifuges();
			next("Selfish" ); scripts.process.compress(c['ğŸ’§'], c['ğŸ¯'], "only: Compactor");
			next("Narcissistic"); craft.make(c['ğŸ¯'], ["HsH","sUs","HsH"], {s: <ore:stickWood>, U: <storagedrawers:upgrade_template>, H: c['ğŸ’§']});

		#-----------------------------------
		# Add missed centrifuge recipes

		val removeBlacklist = [1,3,4,5,7] as int[];
		// val makeTERecipeBlacklist = [10, 9, 12] as int[];

		for i in [1, 3, 4, 5, 7, 10, 11, 12, 15, 16] as int[] {
			if(i >= beesOutputs.length) continue;
			val drop = <gendustry:honey_drop>.definition.makeStack(i);
			val comb = <gendustry:honey_comb>.definition.makeStack(i);
			val beeOuts = beesOutputs[i];
			val honey_drop = i==7 ? drop * 3 : drop;
			if(!(removeBlacklist has i)) {
				mods.thermalexpansion.Centrifuge.removeRecipe(comb);
			}
			scripts.processWork.work(["Centrifuge", "TECentrifuge"], null, [comb], null, [honey_drop], null, [beeOuts[0], beeOuts[1]], [beeHash(i, 1), beeHash(i, 2)]);
		}
	}

	function beeHash(i as int, n as int) as float {
		val nameHash = beesNames[i % beesNames.length].hashCode();
		val variation = ((nameHash / n) % 10) as float; // -9 .. 9
		return 0.5f + variation * 0.05f;
	}

	# TConshard helper
	function shard(mat as string) as IItemStack {
		return <tconstruct:shard>.withTag({Material: mat});
	}

  val c as IItemStack[string] = {
    'ğŸ¯': null, # Current output
    'ğŸ’§': null, # Current honey
    'âš™ï¸': null, # Current Comb
  } as IItemStack[string];

	# Bee combs, honeydrops and outputs based on current index
	function next(s as string) {
    index += 1;
    beesNames += s;
    c['ğŸ¯'] = currOut();
    c['ğŸ’§'] = currHeneyDrop();
    c['âš™ï¸'] = currComb();
  }
	function   currOutList() as IItemStack[] { return beesOutputs[index]; }
	function       currOut() as IItemStack   { return currOutList()[2]; }
	function      currComb() as IItemStack   { return <gendustry:honey_comb>.definition.makeStack(index + 1); }
	function currHeneyDrop() as IItemStack   { return <gendustry:honey_drop>.definition.makeStack(index + 1); }

	function addTinkersCentrifuges() {
		var cuttedArr = [] as IItemStack[];
		for i in 2 to currOutList().length { cuttedArr += currOutList()[i]; }
		scripts.processWork.work(["Centrifuge", "TECentrifuge"], null, [currComb()], null, null, null, cuttedArr, [0.9, 0.8, 0.7, 0.6]);
	}
}

BeeHelper(g).build();
