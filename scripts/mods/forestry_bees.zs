/*

All about Bees, Bees products and beekeeping

*/

#modloaded forestry gendustry

import crafttweaker.item.IIngredient;
import crafttweaker.item.IItemStack;
import crafttweaker.item.WeightedItemStack;
import crafttweaker.liquid.ILiquidStack;
import mods.jei.JEI.removeAndHide as rh;
import scripts.craft.grid.Grid;

// ğŸŸ ğŸŸ¡ğŸŸ¢ğŸŸ£ğŸŸ¤ğŸŸ¥ğŸŸ¦ğŸŸ§ğŸŸ¨ğŸŸ©ğŸŸªğŸŸ«

# Common ingredients
static g as IItemStack[string] = {
	'ğŸŸ¡' : <forestry:beeswax>,
	'ğŸ’›' : <forestry:honey_drop>,
	'ğŸ¤' : <forestry:honeydew>,
	'ğŸŸ¤' : <forestry:propolis>,
	'ğŸŸ ' : <forestry:propolis:1>, # Sticky Propolis
	'ğŸ”´' : <forestry:refractory_wax>,
	'ğŸŸ«' : <forestry:mulch>,
	'ğŸ”·' : <forestry:phosphor>,
	'ğŸ’œ' : <randomthings:ingredient:1>,
	'ğŸ¤' : <minecraft:ghast_tear>,
	'âšª' : <thaumcraft:nugget:5>,
} as IItemStack[string];


# ----------------------------------------------
# Bees rework
# ----------------------------------------------

# Phosphor as melting mechannics
for input,output in {
	<ore:dustPyrotheum>      : <fluid:pyrotheum> * 250,
	<ore:dustCryotheum>      : <fluid:cryotheum> * 250,
	<ore:dustAerotheum>      : <fluid:aerotheum> * 250,
	<ore:dustPetrotheum>     : <fluid:petrotheum> * 250,
	<ore:blockRedstone>      : <fluid:redstone> * 900,
	<ore:glowstone>          : <fluid:glowstone> * 1000,
	<ore:materialEnderPearl> : <fluid:ender> * 250,
	
	<ore:blockCoal>
	|<ore:blockGraphite>     : <fluid:coal> * 900,
} as ILiquidStack[IIngredient]  {
	scripts.processWork.work(["ForestrySqueezer"], null, [g['ğŸ”·'], input], null, null,  [output], null, null);
}

# Buff silk
recipes.removeByRecipeName("forestry:silk_to_string");
recipes.addShapeless("silk to string", <minecraft:string> * 32, [<forestry:crafting_material:2>]);


# Buff Pulsating mesh output
mods.forestry.Carpenter.removeRecipe(<minecraft:ender_pearl>);
scripts.wrap.forestry.Carpenter.addRecipe(<actuallyadditions:block_misc:6> * 4, 
	Grid(["AA","AA"], {A:<forestry:crafting_material:1>}).shaped(), 60);

# Buff propolis to slime (was 1 poor slime ball)
# [Knightslime_Nugget*3] from [Pollen_Cluster][+1]
recipes.removeByRecipeName("forestry:propolis_to_slime");
craft.make(<tconstruct:nuggets:3> * 3, ["pretty",
  "P o P",
  "P o P",
  "P o P"], {
  "P": <forestry:propolis:*>, # Propolis
  "o": <forestry:pollen>      # Pollen Cluster
});


# ---------------------------
# Remake old combs
function reprocessComb(comb as IItemStack, outputs as WeightedItemStack[]) as void {
	mods.forestry.Centrifuge.removeRecipe(comb);
	scripts.wrap.forestry.Centrifuge.addRecipe(outputs, comb, 60);

	if(!comb.matches(<forestry:bee_combs:9>))
		mods.thermalexpansion.Centrifuge.removeRecipe(comb);
	scripts.wrap.thermalexpansion.Centrifuge.addRecipe(outputs, comb, null, 2000);
}

# [Cocoa_Comb]
reprocessComb(<forestry:bee_combs:1>, [
  g['ğŸŸ¡'] % 80, # Beeswax
  <minecraft:dye:3> % 50, # Cocoa
  <nuclearcraft:milk_chocolate> % 20,
]);

# [Stringy_Comb]
reprocessComb(<forestry:bee_combs:3>, [
	g['ğŸŸ '],
	g['ğŸ’›'] % 40,  # Honey Drop
]);

# [Frozen_Comb]
reprocessComb(<forestry:bee_combs:4>, [
	g['ğŸŸ¡'] % 80,     # Beeswax
	g['ğŸ’›'] % 70,  # Honey Drop
	<forestry:pollen:1> % 20,     # Crystalline Pollen Cluster
	<mctsmelteryio:iceball> % 80, # Iceball
]);

# [Dripping_Comb]
reprocessComb(<forestry:bee_combs:5>, [
	g['ğŸ¤'] % 100, # Honeydew
	g['ğŸ’›'] % 40, # Honey Drop
	<thermalfoundation:material:1> % 20, # Gold Dust
]);

# [Parched_Comb]
reprocessComb(<forestry:bee_combs:7>, [
	(<minecraft:blaze_powder> * 2) % 45, # Blaze Powder
	g['ğŸŸ¡'] % 100,  # Beeswax
	g['ğŸ’›'] % 90, # Honey Drop
]);

# [Powdery_Comb]
reprocessComb(<forestry:bee_combs:10>, [
	g['ğŸŸ¡'] % 20,     # Beeswaxer
	g['ğŸ’›'] % 20,  # Honey Drop
	(<minecraft:gunpowder> * 4) % 100, # Gunpowder
]);

# [Mossy_Comb]
reprocessComb(<forestry:bee_combs:15>, [
	g['ğŸŸ¡'] % 100, # Beeswax
	g['ğŸ’›'] % 90, # Honey Drop
	<tconstruct:materials:18> % 100,
]);

# [Irradiated_Comb]
reprocessComb(<forestry:bee_combs:9>, [
  g['ğŸŸ¡'] % 50, # Beeswax
  <nuclearcraft:plutonium:14> % 20, # Tiny Clump of Plutonium-242
  <nuclearcraft:uranium:10> % 80,  # Tiny Clump of Uranium-238
]);

# [Wheaten_Comb]
reprocessComb(<forestry:bee_combs:14>, [
  g['ğŸŸ¡'] % 80, # Beeswax
  (<harvestcraft:flouritem> * 3) % 100,
  <nuclearcraft:graham_cracker> % 80, # Graham Cracker
]);

# [Mellow_Comb]
reprocessComb(<forestry:bee_combs:16>, [
	g['ğŸŸ¡'] % 20, # Beeswax
	g['ğŸ¤'] % 60, # Honeydew
	<ore:dustSoularium>.firstItem % 40,
]);

# Add missed beecombs to JEI (Why they even missed??)
val bee_comb_def = <forestry:bee_combs>.definition;
for i in [1, 8, 9 ,10] as int[] {
  mods.jei.JEI.addItem(bee_comb_def.makeStack(i));
}

# ---------------------------




/*

New Bees mutations

Generated with https://arthursonzogni.com/Diagon/
by data from config/gendustry/bees.cfg

: â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
: â”‚Derpiousâ”‚â”‚GLACIALâ”‚â”‚BOGGYâ”‚â”‚AGRARIANâ”‚â”‚EDENICâ”‚â”‚TRICKYâ”‚â”‚HEROICâ”‚â”‚PHANTASMALâ”‚
: â””â”¬â”€â”€â”€â”€â”¬â”€â”€â”˜â””â”¬â”€â”€â”€â”¬â”€â”€â”˜â””â”¬â”¬â”¬â”€â”€â”˜â””â”¬â”¬â”€â”€â”€â”€â”€â”¬â”˜â””â”¬â”€â”€â”€â”€â”¬â”˜â””â”¬â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
:  â”‚    â”‚    â”‚   â”‚    â”‚â”‚â”‚    â”‚â”‚   â”Œâ”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”‚â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”˜       â”‚          
:  â”‚    â”‚    â”‚   â”‚   â”Œâ”‚â”‚â”‚â”€â”€â”€â”€â”˜â”‚   â”‚â”Œâ”‚â”€â”€â”‚â”€â”€â”€â”€â”‚â”€â”€â”˜               â”‚          
: â”Œâ”˜   â”Œâ”‚â”€â”€â”€â”€â”˜  â”Œâ”‚â”€â”€â”€â”‚â”˜â”‚â”‚ â”Œâ”€â”€â”€â”˜   â”‚â”‚â””â”€â”â”‚    â”‚ â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜          
: â”‚    â”‚â”‚       â”‚â”‚ â”Œâ”€â”‚â”€â”˜â”‚ â”‚â”Œâ”€â”€â”€â”€â”€â”€â”‚â”‚â”€â”€â”‚â”˜    â”‚ â”‚                           
: â”‚â”Œâ”€â”€â”€vvâ”â”Œâ”€â”€â”€â”€â”€vvâ”â”‚â”Œvâ”€â”€vâ”â”‚â”‚â”Œâ”€â”€â”€â”€â”€vvâ”â”Œvâ”€â”€â”€â”€â”€vâ”â”‚                           
: â”‚â”‚Wackyâ”‚â”‚Chilledâ”‚â”‚â”‚Oozyâ”‚â”‚â”‚â”‚Gallantâ”‚â”‚Elysianâ”‚â”‚                           
: â”‚â””â”€â”€â”¬â”€â”¬â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”˜â”‚â””â”¬â”€â”€â”¬â”˜â”‚â”‚â””â”¬â”¬â”€â”€â”€â”¬â”¬â”˜â””â”¬â”€â”€â”€â”€â”€â”€â”˜â”‚                           
: â”‚   â”‚ â”‚  â”‚ â”Œâ”€â”€â”€â”€â”€â”‚â”€â”‚â”€â”€â”‚â”€â”‚â”‚â”€â”˜â”‚   â”‚â”‚  â”‚       â”‚                           
: â”‚   â”‚ â”‚  â”‚ â”‚â”Œâ”€â”€â”€â”€â”‚â”€â”‚â”€â”€â”‚â”€â”˜â”‚  â”‚   â”‚â”‚  â”‚       â”‚                           
: â”‚  â”Œâ”‚â”€â”‚â”€â”€â”˜ â”‚â”‚ â”Œâ”€â”€â”˜ â”‚ â”Œâ”‚â”€â”€â”˜  â”‚   â”‚â”‚â”Œâ”€â”˜       â”‚                           
: â””â” â”‚â”‚ â””â”€â”€â”€â”€â”‚â”‚â”€â”‚â”€â”€â” â”‚ â”‚â””â”€â”€â”  â”‚   â”‚â””â”‚â”€â”     â”Œâ”€â”˜                           
:  â”‚ â”‚â”‚      â”‚â”‚â”Œâ”‚â”€â”€â”‚â”€â”˜ â”‚â”Œâ”€â”€â”‚â”€â”€â”˜   â”‚ â”‚ â”‚     â”‚                             
: â”Œvâ”€vvâ”â”Œâ”€â”€â”€â”€vvvvâ”â”Œvâ”€â”€â”€vvâ”â”Œvâ”€â”€â”€â”€â”€â”€vâ”â”‚â”Œvâ”€â”€â”€â”€â”€vâ”                            
: â”‚Dullâ”‚â”‚Deformedâ”‚â”‚Potterâ”‚â”‚Tinsmithâ”‚â”‚â”‚Paughtyâ”‚                            
: â””â”€â”¬â”¬â”¬â”˜â””â”€â”¬â”€â”¬â”¬â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”€â”€â”˜â”‚â””â”¬â”¬â”€â”€â”€â”€â”€â”˜                            
:   â”‚â”‚â”‚   â”‚ â”‚â”‚    â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”‚â”€â”˜â”‚                                  
:   â””â”‚â”‚â”€â”€â”€â”‚â”€â”‚â”‚â”€â”€â”€â”€â”‚â”€â”€â”€â”    â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”‚â”€â”€â”˜                                  
:    â””â”‚â”€â”€â”€â”‚â”€â”‚â”‚â”€â”  â”‚ â”Œâ”€â”‚â”€â”€â”€â”€â”‚â”€â”€â”€â”€â”€â”€â”€â”€â”˜                                     
: â”Œâ”€â”€â”€vâ”€â”€â”€vâ”â”‚â”‚â”Œvâ”€â”€vâ”â”‚â”Œvâ”€â”€â”€â”€vâ”                                             
: â”‚Egoisticâ”‚â”‚â”‚â”‚Vainâ”‚â”‚â”‚Tinkerâ”‚                                             
: â””â”€â”€â”€â”€â”€â”€â”€â”€â”˜â”‚â”‚â””â”¬â”€â”¬â”¬â”˜â”‚â””â”€â”€â”€â”€â”€â”€â”˜                                             
:       â”Œâ”€â”€â”€â”‚â”‚â”€â”˜ â”‚â”‚ â””â”€â”                                                   
:       â”‚â”Œâ”€â”€â”˜â”‚   â””â”‚â”€â”€â”â”‚                                                   
: â”Œâ”€â”€â”€â”€â”€vvâ”â”Œâ”€vâ”€â”€â”€â”€vâ”â”Œvvâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”                                        
: â”‚Artisanâ”‚â”‚Selfishâ”‚â”‚Narcissisticâ”‚                                        
: â””â”€â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”€â”˜â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜                                        
*/

/*

	Helper class to make several new Bees recipes at once

*/
zenClass BeeHelper {

	# Bees final production
	val beesOutputs as IItemStack[][] = null; 

	# Current bee index
	var index as int = -1 as int;
	var beesNames as string[] = [];

	# Constructor should be called once to register all recipes
	zenConstructor(g as IItemStack[string]) {
		beesOutputs = [
				#Wax,Honeydrop, Main output
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <enderio:item_alloy_nugget:9>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <thermalfoundation:material:770>, <ic2:dust:25>],
				[g['ğŸŸ¡'] ,g['ğŸ¤'], <immersiveengineering:material:17>, <actuallyadditions:item_misc:10>],
				[g['ğŸŸ¡'] ,g['ğŸ¤'], <extendedcrafting:material:37>],
				[g['ğŸŸ¡'] ,g['âšª'], <appliedenergistics2:material:5>],
				[g['ğŸ”´'] ,g['ğŸ¤'], <extrautils2:ingredients:3>],
				[g['ğŸŸ«'] ,g['ğŸ’›'], <thermalfoundation:material:865>],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("psigem"), shard("dreamwood"), shard("flux_crystal"), shard("amethyst")],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("desert_myrmex"), shard("jungle_myrmex"), shard("plague_metal")],
				[g['ğŸ”·'] ,g['ğŸ’›'], <botania:manaresource:1>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <actuallyadditions:item_crystal_empowered:2>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <botania:blacklotus>],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("litherite"), shard("erodium"), shard("kyronite"), shard("lonsdaleite")],
				[g['ğŸ”´'] ,g['ğŸ¤'], shard("pladium"), shard("ionite"), shard("aethium"), shard("boron_nitride")],
				[g['ğŸŸ¡'] ,g['ğŸ’œ'], <extendedcrafting:material:49>],
				[g['ğŸŸ¡'] ,g['ğŸ’›'], <avaritia:resource>],
			] as IItemStack[][];
	}

	function build() {
			next("Wacky"   ); scripts.process.melt(c['ğŸ’§'], <fluid:construction_alloy> * 16, "No exceptions");
			next("Chilled" ); /* Defined in bees.cfg */
			next("Oozy"    ); scripts.process.crush(c['ğŸ’§'], c['ğŸ¯'], "No exceptions", [currOutList()[3]], [0.25]);
			next("Elysian" ); scripts.process.compress(c['ğŸ’§'] * 3, c['ğŸ¯'], "No exceptions");
			next("Gallant" ); furnace.addRecipe(c['ğŸ¯'], c['ğŸ’§']);
			next("Dull"    ); scripts.process.saw(c['âš™ï¸'], c['ğŸ¯'] * 6, "except: shapeless");
			next("Deformed"); craft.shapeless(c['ğŸ¯'], "AAA", {A: c['ğŸ’§']});
			next("Potter"  ); addTinkersCentrifuges();
			next("Tinsmith"); addTinkersCentrifuges();
			next("Ğ aughty" ); mods.botania.ManaInfusion.addInfusion(c['ğŸ¯'], c['ğŸ’§'], 6000/2);
			next("Egoistic"); scripts.wrap.actuallyadditions.AtomicReconstructor.addRecipe(c['ğŸ¯'], c['ğŸ’§'], 10000);
			next("Vain"    ); craft.make(c['ğŸ¯'], [" B ","BHB"," B "], {B: <botania:petal:15>, H: c['ğŸ’§']});
			next("Tinker"  ); addTinkersCentrifuges();
			next("Artisan" ); addTinkersCentrifuges();
			next("Selfish" ); scripts.process.compress(c['ğŸ’§'] * 3, c['ğŸ¯'], "only: Compactor");
			next("Narcissistic"); mods.modularmachinery.RecipeBuilder.newBuilder("amf_diamond_lattice", "advanced_metallurgic_fabricator", 10)
			.addEnergyPerTickInput(100000)
			.addItemOutput(c['ğŸ¯'])
			.addItemInput(c['ğŸ’§'] * 4)
			.addFluidInput(<liquid:diamond> * 666)
			.build();

		# Add missed centrifuge recipes
		for drop in <gendustry:honey_drop>.definition.subItems {
			val i = drop.damage;
			if(i >= beesOutputs.length) continue;
			val comb = <gendustry:honey_comb>.definition.makeStack(i);

			val beeOuts = beesOutputs[i];
			scripts.processWork.work(["Centrifuge"], null, [comb], null, [drop], null, [beeOuts[0], beeOuts[1]], [beeHash(i, 1), beeHash(i, 2)]);
		}
	}

	function beeHash(i as int, n as int) as float {
		val nameHash = beesNames[i % beesNames.length].hashCode();
		val variation = ((nameHash / n) % 10) as float; // -9 .. 9
		return 0.5f + variation * 0.05f;
	}

	# TConshard helper
	function shard(mat as string) as IItemStack {
		return <tconstruct:shard>.withTag({Material: mat});
	}

  val c as IItemStack[string] = {
    'ğŸ¯': null, # Current output
    'ğŸ’§': null, # Current honey
    'âš™ï¸': null, # Current Comb
  } as IItemStack[string];

	# Bee combs, honeydrops and outputs based on current index
	function next(s as string) {
    index += 1;
    beesNames += s;
    c['ğŸ¯'] = currOut();
    c['ğŸ’§'] = currHeneyDrop();
    c['âš™ï¸'] = currComb();
  }
	function   currOutList() as IItemStack[] { return beesOutputs[index]; }
	function       currOut() as IItemStack   { return currOutList()[2]; }
	function      currComb() as IItemStack   { return <gendustry:honey_comb>.definition.makeStack(index + 1); }
	function currHeneyDrop() as IItemStack   { return <gendustry:honey_drop>.definition.makeStack(index + 1); }

	function addTinkersCentrifuges() {
		var cuttedArr = [] as IItemStack[];
		for i in 2 to currOutList().length { cuttedArr += currOutList()[i]; }
		scripts.processWork.work(["Centrifuge", "TECentrifuge"], null, [currComb()], null, null, null, cuttedArr, [0.9, 0.8, 0.7, 0.6]);
	}
}

BeeHelper(g).build();